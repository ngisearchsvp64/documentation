% SPDX-License-Identifier: LGPL-3-or-later
% Copyright 2023 VectorCamp
% Copyright 2023 Red Semiconductor Ltd.
% Copyright 2023 VanTosh
%
% Funded by NGI Search Programme HORIZON-CL4-2021-HUMAN-01 2022,
% https://www.ngisearch.eu/, EU Programme 101069364.

\section{Optimised \texttt{memchr} Function}

\subsection{The code}

% TODO: Use Github link
The function was implemented within Python framework can be found
\href{https://git.vantosh.com/ngisearch/glibc-svp64/src/commit/fd6de0a7fd050230eaf891abfd510ca83cd8ab62/svp64-port/optimised_variants/test_memchr.py}{here}.
\begin{verbatim}
  cmpi 0,1,5,0 # Check if n==0
  bc 12, 2, .no_match # 0x44
  mtspr 9, 5 # move r5 to CTR
  add 7,3,5 # start address+len
  # start + len + 2 (if this is final pointer val, no match)
  addi 7,7,2
  setvl 1,0,4,0,1,1
  # load VL bytes (update r3 addr, current pointer)
.inner:
  sv.lbzu/pi *16, 1(3)
  # cmp against zero, truncate VL
  sv.cmp/ff=eq/vli *0,1,*16,4
  # test CTR, stop if any cmp failed
  sv.bc/all 0, *2, .inner # -0x10

  # Check for no match, add offset to get actual found address
  # If pointer just outside of array, no match.
  cmp 0,1,3,7"
  bc 12, 2, .no_match # 0x14

  # Adjust pointer in Reg 3 because search ends at multiples of
  # VL value
  setvl 6,0,1,0,0,0", # Get current value of VL
  addi 6,6,-5" # ((-1*maxvl)-1) = (-1*4)-1 = -5, calc offset for found char
  # Reg 3 will now be found address, or one byte outside of array
  add 3,3,6
  b .end # 0x8

.no_match:
  # No match, return null for matched address
  addi 3,0,0
.end:
  # this could be replaced by return call
  nop
\end{verbatim}

\subsection{Code explanation}
\begin{verbatim}
cmpi 0,1,5,0 # Check if n==0
bc 12, 2, .no_match # 0x44
\end{verbatim}
The first part is just a check for $n=0$ case (no need to run function if
there's no array).

\begin{verbatim}
mtspr 9, 5 # move r5 to CTR
\end{verbatim}
Load the length of the byte array from integer register 5 into
the Count Register (CTR), which is used to keep track of loop iterations
(standard use in PowerISA).

\begin{verbatim}
add 7,3,5 # start address+len
# start + len + 2 (if this is final pointer val, no match)
addi 7,7,2
\end{verbatim}
This calculates the address value after the last byte in the array, which is
used to check if no match occurred. This end address is stored in register 7.

\begin{verbatim}
  setvl 1,0,4,0,1,1
  # load VL bytes (update r3 addr, current pointer)
.inner:
  sv.lbzu/pi *16, 1(3)
  # cmp against zero, truncate VL
  sv.cmp/ff=eq/vli *0,1,*16,4
  # test CTR, stop if any cmp failed
  sv.bc/all 0, *2, .inner # -0x10
\end{verbatim}
This is the main bit of SVP64 code (running in Vertical-First mode).
The snippet of code will iterate 4 times as specified by immediate
in \texttt{setvl} (checking one byte with each iteration).
Load byte from memory at address stored in register 3.
The Load Byte and Zero with Update (\texttt{lbzu}) will automatically increment
the address stored in register 3 with the specified offset (+1 in this case).

The compare instruction will check the loaded byte with the specified char
(stored in register 4).

A special mode of Simple-V called Data-Dependent Fail First (FF) is used with
the compare instruction. FF works by checking given data, and truncating the
Vector Length (VL) if the condition is met. The condition here is that
loaded byte is equal (\texttt{ff=eq}) to the specified byte in register 4.
The additional VL inclusive (\texttt{vli}) option ensures the vector is
truncated at the matched byte (since it needs its address is what's required
from \texttt{memchr}).

The conditional branch (\texttt{sv.bc}) in \texttt{BO=0} mode will decrement
CTR, then branch only if CTR is non-zero (haven't finished searching the
byte array), \textit{and} the Condition Field 0 bit 2 (EQ) is cleared
(not matching character).

\begin{verbatim}
# Check for no match, add offset to get actual found address
# If pointer just outside of array, no match.
cmp 0,1,3,7
bc 12, 2, .no_match # 0x14
\end{verbatim}

Compare the current address at register 3 with the address just outside
the byte array for equality. This can only occur if the whole array was
searched, and there was no match. In such a case jump to \texttt{.no\_match}.

\begin{verbatim}
# Adjust pointer in Reg 3 because search ends at multiples of
# VL value
setvl 6,0,1,0,0,0", # Get current value of VL
addi 6,6,-5" # ((-1*maxvl)-1) = (-1*4)-1 = -5, cal offset for found char
# Reg 3 will now be found address, or one byte outside of array
add 3,3,6
b .end # 0x8
\end{verbatim}

When a match occurs, VL will be truncated to a value between 0 and 3 (since VL
was set to 4 earlier), and for this to be converted to a final match address,
some arithmetic needs to be done.

The current VL value is stored in a temporary register 6. Adding it to a
precomputed offset and the current address will produce the correct address
value at register 3.

\begin{verbatim}
.no_match:
  # No match, return null for matched address
  addi 3,0,0
.end:
  # this could be replaced by return call
  nop
\end{verbatim}

In the case of a no match, register 3 is cleared (return address set to null)
before being returned.

The main difference between the implementation described in IMP2 and the one
shown above, is that to use the more advanced features of SVP64, one has to
think more in terms of C-like code (one element at a time),
as opposed to SIMD (blocks of power-of-2 elements). The initial
implementation processed the byte in chunks of 8-bytes (by loading 8 bytes
from memory into a single 64-bit register), which then required a further
sub-search once a match had been detected.

Although the code size is indeed smaller, the actual throughput (number of
bytes checked per second etc.) is dependent on whether the underlying hardware
supporting SVP64 can recognise the above algorithm and run it in parallel.
